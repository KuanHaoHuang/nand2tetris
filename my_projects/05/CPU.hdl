// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:

    // input
    Or16(a=instruction, b=instruction, 
         out[15]=isC,
         out[12]=instruction12,
         out[11]=instruction11,
         out[10]=instruction10,
         out[9]=instruction9,
         out[8]=instruction8,
         out[7]=instruction7,
         out[6]=instruction6,
         out[5]=instruction5,
         out[4]=instruction4,
         out[3]=instruction3,
         out[2]=instruction2,
         out[1]=instruction1,
         out[0]=instruction0);
    Not(in=isC, out=isA);

    // A register and its input mux
    Mux16(sel=isA, a=aluOut, b=instruction, out=aMuxOut);
    And(a=isC, b=instruction5, out=isCAndIns5);
    Or(a=isA, b=isCAndIns5, out=aRegLoad);
    ARegister(in=aMuxOut, load=aRegLoad, 
              out=aRegOut, out[0..14]=addressM);

    // D register
    And(a=isC, b=instruction4, out=dRegLoad);
    DRegister(in=aluOut, load=dRegLoad, out=dRegOut);

    // b Mux
    Mux16(a=aRegOut, b=inM, sel=instruction12, out=bMuxOut);

    // ALU
    ALU(x=dRegOut, y=bMuxOut, 
        zx=instruction11,
        nx=instruction10,
        zy=instruction9,
        ny=instruction8,
        f=instruction7,
        no=instruction6,
        out=outM,
        out=aluOut,
        zr=aluZrOut, 
        ng=aluNgOut);
    And(a=isC, b=instruction3, out=writeM);

    // PC
    And(a=instruction2, b=aluNgOut, out=jp1);
    And(a=instruction1, b=aluZrOut, out=jp2);
    Or(a=aluNgOut, b=aluZrOut, out=orNgZr);
    Not(in=orNgZr, out=notOrNgZr);
    And(a=instruction0, b=notOrNgZr, out=jp3);
    Or8Way(in[0]=jp1, in[1]=jp2, in[2]= jp3, in[3]=false, out=orJp);
    And(a=isC, b=orJp, out=pcLoad);
    PC(in=aRegOut, inc=true, load=pcLoad, reset=reset, out[0..14]=pc);
}
